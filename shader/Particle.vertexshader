#version 330 core

// Input vertex data, different for all executions of this shader.
layout(location = 0) in vec3 squareVertices;
layout(location = 1) in vec4 xyzs; // Position of the center of the particule and size of the square
layout(location = 2) in vec4 color; // Position of the center of the particule and size of the square

// Output data ; will be interpolated for each fragment.
out vec2 UV;
out vec2 relativePos;
out vec4 particlecolor;

// Values that stay constant for the whole mesh.
uniform vec3 CameraRight_worldspace;
uniform vec3 CameraUp_worldspace;
uniform vec3 CameraPos_worldspace;
uniform vec3 Center_worldspace;
uniform mat4 VP; // Model-View-Projection matrix, but without the Model (the position is in BillboardPos; the orientation depends on the camera)

void main()
{
	float particleSize = xyzs.w; // because we encoded it this way.
	vec3 particleCenter_wordspace = xyzs.xyz;
	
	vec3 vertexPosition_worldspace = 
		particleCenter_wordspace
		+ CameraRight_worldspace * squareVertices.x * particleSize
		+ CameraUp_worldspace * squareVertices.y * particleSize;

	vec3 centerToParticle = vertexPosition_worldspace - Center_worldspace;
	vec3 cameraToCenter = Center_worldspace - CameraPos_worldspace;
	vec2 scaleRate = vec2(0.6,0.6);
	relativePos = vec2(dot(centerToParticle,cross(normalize(cameraToCenter),CameraUp_worldspace)),dot(centerToParticle,cross(CameraRight_worldspace,normalize(cameraToCenter)))) * scaleRate + vec2(0.5, 0.5);
	//relativePos = vec2(dot(centerToParticle,CameraRight_worldspace),dot(centerToParticle,CameraUp_worldspace)) * scaleRate + vec2(0.5, 0.5);
	// Output position of the vertex
	gl_Position = VP * vec4(vertexPosition_worldspace, 1.0f);

	// UV of the vertex. No special space for this one.
	UV = squareVertices.xy + vec2(0.5, 0.5);
	particlecolor = color;
}

