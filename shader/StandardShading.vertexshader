#version 330 core

// Input vertex data, different for all executions of this shader.
layout(location = 0) in vec3 vertexPosition_modelspace;
layout(location = 1) in vec2 vertexUV;
layout(location = 2) in vec3 vertexNormal_modelspace;
layout(location = 3) in vec3 vertexTangent_modelspace;
layout(location = 4) in vec3 vertexBitangent_modelspace;

// Output data ; will be interpolated for each fragment.
out vec2 UV;
out vec3 Position_worldspace;
out vec3 Normal_cameraspace;
out vec2 Normal_clipspace;
out vec3 Normal_lightspace;

out vec3 EyeDirection_worldspace;
out vec3 LightDirection_worldspace;

out vec3 TorchDirection_tangentspace;
out vec3 LightDirection_tangentspace;
out vec3 EyeDirection_tangentspace;

out vec4 PointCoord;
out vec4 ShadowCoord;
// Values that stay constant for the whole mesh.
uniform mat4 MVP;
uniform mat4 DepthBiasMVP;
uniform mat4 DepthMVP;
uniform mat4 DepthMV;
uniform mat4 V;
uniform mat4 M;
uniform mat3 MV3x3;
uniform vec3 LightPosition_worldspace;
uniform vec3 TorchPosition_worldspace;
uniform vec3 eyePosWP;
void main(){
	
	// Output position of the vertex, in clip space : MVP * position
	gl_Position =  MVP * vec4(vertexPosition_modelspace,1);
	PointCoord = gl_Position;
	ShadowCoord = DepthBiasMVP * vec4(vertexPosition_modelspace,1);
	Normal_lightspace = (DepthMVP * vec4(vertexNormal_modelspace,0)).xyz;
	// Position of the vertex, in worldspace : M * position

	Position_worldspace = (M * vec4(vertexPosition_modelspace,1)).xyz;
	EyeDirection_worldspace =  eyePosWP - Position_worldspace;
	LightDirection_worldspace = Position_worldspace - LightPosition_worldspace;

	// Vector that goes from the vertex to the camera, in camera space.
	// In camera space, the camera is at the origin (0,0,0).
	vec3 vertexPosition_cameraspace = ( V * M * vec4(vertexPosition_modelspace,1)).xyz;
	vec3 EyeDirection_cameraspace = vec3(0,0,0) - vertexPosition_cameraspace;
	// Vector that goes from the vertex to the light, in camera space. M is ommited because it's identity.
	vec3 LightPosition_cameraspace = ( V * vec4(TorchPosition_worldspace,1)).xyz;
	vec3 TorchPosition_cameraspace = ( V * vec4(TorchPosition_worldspace,1)).xyz;
	vec3 LightDirection_cameraspace = LightPosition_cameraspace + EyeDirection_cameraspace;
	vec3 TorchDirection_cameraspace = TorchPosition_cameraspace + EyeDirection_cameraspace;
	// Normal of the the vertex, in camera space
	Normal_cameraspace = ( V * M * vec4(vertexNormal_modelspace,0)).xyz; // Only correct if ModelMatrix does not scale the model ! Use its inverse transpose if not.
	Normal_clipspace = (normalize(MVP * vec4(vertexNormal_modelspace,0))).xy;
	// UV of the vertex. No special space for this one.
	UV = vertexUV;

	// model to camera = ModelView
	vec3 vertexTangent_cameraspace = MV3x3 * vertexTangent_modelspace;
	vec3 vertexBitangent_cameraspace = MV3x3 * vertexBitangent_modelspace;
	vec3 vertexNormal_cameraspace = MV3x3 * vertexNormal_modelspace;

	mat3 TBN = transpose(mat3(
		vertexTangent_cameraspace,
		vertexBitangent_cameraspace,
		vertexNormal_cameraspace	
	)); // You can use dot products instead of building this matrix and transposing it. See References for details.

	LightDirection_tangentspace = TBN * LightDirection_cameraspace;
	EyeDirection_tangentspace =  TBN * EyeDirection_cameraspace;
	TorchDirection_tangentspace = TBN * TorchDirection_cameraspace;
}

